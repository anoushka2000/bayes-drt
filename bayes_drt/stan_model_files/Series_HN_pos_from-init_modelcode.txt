functions {
	real HN_x(real omega, real tau0, real beta) {
		// Intermediate "x" variable
		return 1+ pow(omega*tau0,beta)*cos(beta*pi()/2);
	}
	real HN_y(real omega, real tau0, real beta){
		// Intermediate "y" variable
		return pow(omega*tau0,beta)*sin(beta*pi()/2);
	}
	real Zmod_HN(real alpha, real x, real y) {
		return pow(sqrt(square(x) + square(y)),-alpha);
	}
	real theta_HN(real alpha, real x, real y) {
		return -alpha*atan2(y,x);
	}
}
data {
	// dimensions
	int<lower=0> N; // number of measured frequencies
	int<lower=0> K; // number of HN basis functions
	
	// impedance data
	vector[2*N] Z; // stacked impedance vector ([Z' Z'']^T)
	vector[N] freq; //measured frequencies
	
	// initial guess for ln(tau)
	vector[K] lntau_guess;
	real<lower=0> lntau_uncertainty;
	real<lower=0> lntau_shift_scale;
	
	// fixed hyperparameters
	real<lower=0> sigma_min; // noise level floor
	real<lower=0> induc_scale; // inductance scale
	real<lower=0> R_scale_alpha;
	real<lower=0> R_scale_beta;
}
transformed data {
	vector [N] omega = freq*2*pi();
}
parameters {
	// impedance offsets
	real<lower=0> Rinf_raw;
	real<lower=0> induc_raw;
	
	// HN parameters
	vector<lower=0>[K] R_HN;
	vector<lower=0>[K] R_scale;
	vector<lower=-lntau_uncertainty/lntau_shift_scale,upper=lntau_uncertainty/lntau_shift_scale>[K] lntau_shift_raw;
	vector<lower=0,upper=1>[K] alpha_HN;
	vector<lower=0,upper=1>[K] beta_HN;
	
	// error structure parameters
	real<lower=0> sigma_res_raw;
	real<lower=0> alpha_prop_raw;
	real<lower=0> alpha_re_raw;
	real<lower=0> alpha_im_raw;
	
}
transformed parameters {
	// impedance offsets
	real<lower=0> Rinf = Rinf_raw*100;
	real<lower=0> induc = induc_raw*induc_scale;
	
	// HN parameters
	vector<lower=-lntau_uncertainty,upper=lntau_uncertainty>[K] lntau_shift = lntau_shift_raw*lntau_shift_scale;
	vector[K] lntau_HN = lntau_guess + lntau_shift;
	
	// error structure parameters
	real<lower=0> sigma_res = sigma_res_raw*0.05;
	real<lower=0> alpha_prop = alpha_prop_raw*0.05;
	real<lower=0> alpha_re = alpha_re_raw*0.05;
	real<lower=0> alpha_im = alpha_im_raw*0.05;
	vector<lower=0>[2*N] sigma_tot; 
	
	// impedance vectors
	vector[2*N] Z_hat;
	vector[N] Z_hat_re = rep_vector(0,N);
	vector[N] Z_hat_im = rep_vector(0,N);
	
	
	// calculate Z_hat
	for (k in 1:K){
		real tau_HN = exp(lntau_HN[k]);
		for (n in 1:N){
			real x = HN_x(omega[n],tau_HN,beta_HN[k]);
			real y = HN_y(omega[n],tau_HN,beta_HN[k]);
			real Zmod = Zmod_HN(alpha_HN[k],x,y);
			real theta = theta_HN(alpha_HN[k],x,y);
			Z_hat_re[n] += R_HN[k]*Zmod*cos(theta);
			Z_hat_im[n] += R_HN[k]*Zmod*sin(theta);
		}
	}
	
	Z_hat_re += Rinf;
	Z_hat_im += induc*omega;
	Z_hat = append_row(Z_hat_re,Z_hat_im);
	
	// calculate error scale
	sigma_tot = sqrt(square(sigma_min) + square(sigma_res) + square(alpha_prop*Z_hat)
									+ square(alpha_re*append_row(Z_hat_re,Z_hat_re)) 
									+ square(alpha_im*append_row(Z_hat_im,Z_hat_im))
									);
}
model {
	// impedance offsets
	Rinf_raw ~ std_normal();
	induc_raw ~ std_normal();
	
	// HN parameters
	lntau_shift_raw ~ std_normal();
	R_scale ~ inv_gamma(R_scale_alpha,R_scale_beta);
	R_HN ~ normal(0,R_scale);
	
	// impedance
	Z ~ normal(Z_hat,sigma_tot);
	
	// error structure
	sigma_res_raw ~ std_normal();
	alpha_prop_raw ~ std_normal();
	alpha_re_raw ~ std_normal();
	alpha_im_raw ~ std_normal();
}